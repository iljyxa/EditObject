
&НаКлиенте
Процедура ОткрытьФормуОбъекта(Команда)
	ОткрытьФорму("ВнешняяОбработка.РедактированиеОбъекта.Форма.ФормаОбъекта", Новый Структура("Ссылка", Ссылка), ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.Независимый);
КонецПроцедуры

&НаКлиенте
Процедура ВставитьСсылку(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВводСсылкиЗавершение", ЭтотОбъект);
	ПоказатьВводСтроки(ОписаниеОповещения,, "Введите строку, полученную с помощью команды ""Получить ссылку"" (Ctrl+F11)",, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ВводСсылкиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Результат) Тогда
		Возврат;	
	КонецЕсли;
	
	Ссылка = ПолучитьСсылкуНаОбъектИзНавигационнойСсылки(СокрЛП(Результат));
	
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось разобрать навигационную ссылку";
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСсылкуНаОбъектИзНавигационнойСсылки(НавигационнаяСсылка)
    
    ПерваяТочка = Найти(НавигационнаяСсылка, "e1cib/data/");
    ВтораяТочка = Найти(НавигационнаяСсылка, "?ref=");
	
	Если ПерваяТочка = 0 ИЛИ ВтораяТочка = 0 Тогда
		Возврат Неопределено;	
	КонецЕсли; 
	
    ПредставлениеТипа   = Сред(НавигационнаяСсылка, ПерваяТочка + 11, ВтораяТочка - ПерваяТочка - 11);
    ШаблонЗначения = ЗначениеВСтрокуВнутр(ПредопределенноеЗначение(ПредставлениеТипа + ".ПустаяСсылка"));
    ЗначениеСсылки = СтрЗаменить(ШаблонЗначения, "00000000000000000000000000000000", Сред(НавигационнаяСсылка, ВтораяТочка + 5));
    
	Возврат ЗначениеИзСтрокиВнутр(ЗначениеСсылки);
    
КонецФункции